/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow'
	id 'com.google.cloud.tools.jib'
}

description = 'Examples for using java auto instrumentation and Google Cloud Operations'

// examples are not published, so version can be hardcoded
version = '0.1.0'

configurations {
	agent
}

// the shaded variant name for gcp auth extension
def gcp_auth_extension_shaded_classifier = 'shadow'

dependencies {
	agent agentLibraries.agent
	agent project(path: ':exporter-auto', configuration: 'shadow')
	agent "${libraries.opentelemetry_gcp_auth_extension}:${gcp_auth_extension_shaded_classifier}"

	implementation(libraries.spring_boot_starter_web)
	implementation(libraries.spring_cloud_starter_openfeign)
	// Temporary, for log-trace correlation
	implementation(libraries.opentelemetry_api)
	implementation('com.google.cloud:google-cloud-core:2.0.5')
}

task copyAgent(type: Copy) {
	into layout.buildDirectory.dir("otelagent")
	from configurations.agent {
		rename "opentelemetry-javaagent(.*).jar", "otel_agent.jar"
		rename "exporter-auto(.*).jar", "gcp_exporter_ext.jar"
		rename "opentelemetry-gcp-auth-extension(.*).jar", "gcp_auth_ext.jar"
	}
}

jib {
	from.image = 'gcr.io/distroless/java-debian10:11'
	containerizingMode = 'packaged'
	container.ports = ["8080"]
	extraDirectories {
		paths {
			path {
				from = layout.buildDirectory.dir("otelagent")
				into = '/otelagent'
			}
		}
	}
	// TODO: Figure out how to pull versions from build.
	container.jvmFlags = [
		// Use the downloaded java agent.
		'-javaagent:/otelagent/otel_agent.jar',
		// Use the GCP exporter extensions.
		'-Dotel.javaagent.extensions=/otelagent/gcp_exporter_ext.jar,/otelagent/gcp_auth_ext.jar',
		// Configure auto instrumentation.
		// Configure traces to be exported via OTLP exporters
		'-Dotel.traces.exporter=otlp',
		'-Dotel.exporter.otlp.endpoint=https://telemetry.googleapis.com',
		'-Dotel.exporter.otlp.traces.protocol=http/protobuf',
		// Configure metrics to be exported via Google cloud exporters
		'-Dotel.metrics.exporter=google_cloud_monitoring',
		'-Dotel.logs.exporter=none',
		'-Dotel.service.name=gcp-autoinstrument-spring-demo',
		'-Dotel.resource.providers.gcp.enabled=true'
	]
}

application {
	mainClassName = 'com.google.example.TestMain'
}

tasks.named('jib').configure {
	dependsOn copyAgent
}

tasks.named('jibDockerBuild').configure {
	dependsOn copyAgent
}

tasks.named('jibBuildTar').configure {
	dependsOn copyAgent
}