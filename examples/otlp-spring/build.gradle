/*
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
	id 'java'
	id 'application'
	id 'com.google.cloud.tools.jib'
}

description = 'Example showcasing OTLP trace ingest on GCP and Google Auth in a Spring Boot App'

// examples are not published, so version can be hardcoded
version = '0.1.0'

repositories {
	mavenCentral()
}

dependencies {
	implementation(libraries.opentelemetry_api)
	implementation(libraries.opentelemetry_sdk)
	implementation(libraries.opentelemetry_otlp_exporter)
	implementation(libraries.opentelemetry_sdk_autoconf)
	implementation(libraries.opentelemetry_gcp_auth_extension)
	implementation(libraries.opentelemetry_gcp_resources)

	implementation(libraries.spring_boot_starter_web)
}

def autoconf_config = [
	'-Dgoogle.cloud.project=your-gcp-project-id',
	'-Dotel.exporter.otlp.endpoint=https://telemetry.googleapis.com',
	'-Dotel.traces.exporter=otlp',
	'-Dotel.metrics.exporter=none',
	'-Dotel.exporter.otlp.protocol=http/protobuf',
	'-Dotel.java.global-autoconfigure.enabled=true',
	'-Dotel.service.name=spring-otlp-trace-example-service',
]

application {
	mainClass = "com.google.cloud.opentelemetry.examples.otlpspring.Main"
	applicationDefaultJvmArgs = autoconf_config
}

jib {
	from.image = "gcr.io/distroless/java-debian10:11"
	containerizingMode = "packaged"
	container.jvmFlags = autoconf_config as List
	container.ports = ["8080"]
}

tasks.register('runClient', JavaExec) {
	classpath = sourceSets.test.runtimeClasspath
	mainClass = 'com.google.cloud.opentelemetry.examples.otlpspring.MainClient'
}
