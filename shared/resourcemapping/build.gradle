/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
	id "maven-publish"
	id "com.github.johnrengelman.shadow"
}


description = 'Resource helper utilites for GCP within OpenTelemetry'

dependencies {
	api(libraries.auto_value_annotations)
	api(libraries.opentelemetry_api)
	api(libraries.opentelemetry_sdk_common)
	annotationProcessor(libraries.auto_value)
	implementation(libraries.opentelemetry_semconv)
	implementation platform(libraries.opentelemetry_bom)
	testImplementation(testLibraries.junit)
	testImplementation(libraries.opentelemetry_semconv)
}

shadowJar{
	archiveClassifier.set('')
	relocate 'io.opentelemetry.semconv', 'com.google.cloud.opentelemetry.shadow.semconv'
	dependencies {
		exclude(dependency(libraries.opentelemetry_api))
		exclude(dependency(libraries.auto_value_annotations))
		exclude(dependency(libraries.opentelemetry_sdk_common))
		exclude(dependency(libraries.opentelemetry_context))
	}
	mergeServiceFiles()
}

publishing {
	publications {
		maven(MavenPublication) { publication ->
			from project.shadow.component(publication)
			artifact sourcesJar
			artifact javadocJar
			components.java.withVariantsFromConfiguration(configurations.runtimeElements) {
				skip()
			}
		}
	}
}

// This is to fix the explicit dependency error which comes when publishing via the `candidate` task
publishMavenPublicationToMavenRepository.dependsOn jar
